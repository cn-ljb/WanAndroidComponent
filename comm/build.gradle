plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def isDebug = rootProject.ext.isDebug

def application  = rootProject.ext.application
def project_appId = rootProject.ext.appId.app

def dependenciesImp = rootProject.ext.dependenciesImp
def dependenciesKapt = rootProject.ext.dependenciesKapt

android {
    compileSdkVersion application.compileSdkVersion
    buildToolsVersion application.buildToolsVersion

    defaultConfig {
        minSdkVersion application.minSdkVersion
        targetSdkVersion application.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        //将当前构建环境状态写入 BuildConfig 文件中
        buildConfigField("String", "APP_ID", "\"" + String.valueOf(project_appId) + "\"")
        if (isDebug) {
            buildConfigField("String", "HTTP_HOST", "\"" + String.valueOf(rootProject.ext.HTTP_HOST_DEBUG) + "\"")
        } else {
            buildConfigField("String", "HTTP_HOST", "\"" + String.valueOf(rootProject.ext.HTTP_HOST_RELEASE) + "\"")
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }


        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    viewBinding {
        enabled = true
    }
    resourcePrefix 'comm_'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    dependenciesImp.each { k, v -> implementation(v) }
    dependenciesKapt.each { k, v -> kapt(v) }

    //leakcanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${rootProject.ext.leakcanary_version}"
    //dokit
    debugImplementation "com.didichuxing.doraemonkit:dokitx:${rootProject.ext.dokit_version}"
    releaseImplementation "com.didichuxing.doraemonkit:dokitx-no-op:${rootProject.ext.dokit_version}"
}
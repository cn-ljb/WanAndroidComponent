// 取出isComponent字段
def isComponent = rootProject.ext.isComponent.home
def isDebug = rootProject.ext.isDebug

// 根据isRelease字段，来确定当前是Library还是可执行Module
if (isComponent) {
    apply plugin: 'com.android.application'
    apply plugin: 'com.didi.dokit'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


def appId = rootProject.ext.appId.home
def project_appId = rootProject.ext.appId.app
def application = rootProject.ext.application
def dependenciesImp = rootProject.ext.dependenciesImp
def dependenciesKapt = rootProject.ext.dependenciesKapt


android {
    compileSdkVersion application.compileSdkVersion
    buildToolsVersion application.buildToolsVersion

    defaultConfig {
        if (isComponent) {
            applicationId appId
        }
        minSdkVersion application.minSdkVersion
        targetSdkVersion application.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        //将当前构建环境状态写入 BuildConfig 文件中
        buildConfigField("boolean", "IS_COMPONENT", String.valueOf(isComponent))
        buildConfigField("String", "APP_ID", "\"" + String.valueOf(project_appId) + "\"")
        if (isDebug) {
            buildConfigField("String", "HTTP_HOST", "\"" + String.valueOf(rootProject.ext.HTTP_HOST_DEBUG) + "\"")
        } else {
            buildConfigField("String", "HTTP_HOST", "\"" + String.valueOf(rootProject.ext.HTTP_HOST_RELEASE) + "\"")
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

        //java EventBus索引
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: appId + ".MyEventBusIndex"]
            }
        }
        // kotlin EventBus索引
        kapt {
            arguments {
                arg('eventBusIndex', appId + ".MyEventBusIndex")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            // 独立调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isComponent) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/_ReleaseManifest/AndroidManifest.xml'
            }
        }
    }

    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //公共模块所有子模块都引入
    implementation project(path: ':comm')
    dependenciesImp.each { k, v -> implementation(v) }
    dependenciesKapt.each { k, v -> kapt(v) }

    //leakcanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${rootProject.ext.leakcanary_version}"
    //dokit
    debugImplementation "com.didichuxing.doraemonkit:dokitx:${rootProject.ext.dokit_version}"
    releaseImplementation "com.didichuxing.doraemonkit:dokitx-no-op:${rootProject.ext.dokit_version}"

    //组件环境，开发调试过程中，如需要其他模块配合，可将需引入模块转化为集成环境，单独引入
    //注：单独引入情况下，需注意 解耦！解耦！！解耦！！！
    // 可在调试完成后，注掉单独引入的模块，看看是否存在耦合问题
    if (isComponent) {
//        implementation project(path: ':user')
    }
}

if (isComponent) {
    dokitExt {
        //通用设置
        comm {
            //地图经纬度开关
            gpsSwitch true
            //网络开关
            networkSwitch true
            //大图开关
            bigImgSwitch true
            //webView js 抓包
            webViewSwitch true
        }

        slowMethod {
            //调用栈模式配置
            stackMethod {
                //默认值为 5ms 小于该值的函数在调用栈中不显示
                thresholdTime 10
                //调用栈函数入口
                enterMethods = ["com.ljb.android.comm.onCreate"]
                //黑名单 粒度最小到类 暂不支持到方法
                methodBlacklist = []
            }
            //普通模式配置
            normalMethod {
                //默认值为 500ms 小于该值的函数在运行时不会在控制台中被打印
                thresholdTime 500
                //需要针对函数插装的包名
                packageNames = [appId]
                //不需要针对函数插装的包名&类名
                methodBlacklist = []
            }
        }
    }
}